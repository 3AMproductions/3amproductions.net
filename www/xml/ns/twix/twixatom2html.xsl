<?xml version="1.0" encoding="UTF-8"?>
<?altova_samplexml twilight\portfolio.atom.xml?>
<xsl:stylesheet version="1.0"  exclude-result-prefixes="xsi atom dc xhtml" extension-element-prefixes="date" 
	xmlns="http://www.w3.org/1999/xhtml" 
	xmlns:xhtml="http://www.w3.org/1999/xhtml" 
	xmlns:xsl="http://www.w3.org/1999/XSL/Transform" 
	xmlns:xsi="http://www.w3.org/2001/XMLSchema-instance" 
	xmlns:atom="http://www.w3.org/2005/Atom" 
	xmlns:dc="http://purl.org/dc/elements/1.1/"
	xmlns:date="http://exslt.org/dates-and-times">
<!-- It would be nice to use the first .xsl (in includes) but can't reference outside the webroot from browser when doing browser-side transforms. -->
<!-- <xsl:import href="../../../../includes/exslt/date/functions/format-date/date.format-date.function.xsl"/> -->
    <xsl:import href="../../exslt/date/functions/format-date/date.format-date.function.xsl"/>
	<xsl:output method="xml" version="1.0" encoding="UTF-8" doctype-public="-//W3C//DTD XHTML 1.0 Strict//EN" doctype-system="http://www.w3.org/TR/xhtml1/DTD/xhtml1-strict.dtd" media-type="application/xhtml+xml" indent="yes" omit-xml-declaration="no"/>
	<xsl:template match="atom:feed">
		<html>
			<head profile="http://microformats.org/wiki/hatom#XMDP_Profile http://microformats.org/wiki/rel-license#XMDP_profile http://microformats.org/wiki/hcard-profile http://microformats.org/wiki/rel-tag http://3amproductions.net/xml/xmdp.php">
				<title>
					<xsl:value-of select="atom:title"/>
				</title>
				<xsl:apply-templates select="atom:link[contains(concat(' ',@rel,' '),' self ')]" mode="self" />
				<xsl:apply-templates select="atom:link[contains(concat(' ',@rel,' '),' alternate ')]" mode="alternate" />
				<!-- <script type="text/javascript" src="/scripts/lib.prototype.js.php"></script>
				<script type="text/javascript" src="/scripts/lib.dom.js"></script>
				<script type="text/javascript" src="/scripts/footnotelinks.js"></script> -->
				<link rel="stylesheet" media="projection,screen" href="/styles/twix.feed.html.css" type="text/css" />
				<link rel="stylesheet" media="print" href="/styles/print.css" type="text/css" />
				<link rel="stylesheet" media="handheld" href="/styles/handheld.css" type="text/css" />
				<link rel="shortcut icon" href="{atom:icon}" type="image/x-icon" />
			</head>
			<body class="hfeed">
				<div id="header">
					<h1 id="vcard-3am" class="vcard"><a class="include" href="#logo"></a><span class="fn org"><span id="orgname" class="organization-name">3AM Productions</span></span> ||| <span id="note" class="note">We Make Websites</span></h1>
				</div>
				<ul id="nav" title="Main Navigation">
					<li id="skipnav"><a href="#content" title="Skip Navigation">Skip to Content</a></li>
					<li><a href="/index" title="go to our homepage" rel="home">Home</a></li>
					<li><a href="/approach" title="follow our web development procedure">Procedure</a></li>
					<li><a href="/about" title="learn more about 3AM" rel="prev about">About</a></li>
					<li><a href="/portfolio" class="active" title="see some of our previous work">Portfolio</a></li>
					<li><a href="/contact" title="get in touch with us" rel="next contact">Contact</a></li>
				</ul>
				<div id="content">
					<h1>
						<xsl:value-of select="atom:title"/>
					</h1>
					<p><a class="addthis" href="http://www.addthis.com/feed.php?pub=ZDUU69DWLFL07CV4&amp;h1=http%3A%2F%2F3amproductions.net%2Fatom&amp;t1=" title="Subscribe using any feed reader!"><img src="http://s9.addthis.com/button2-fd.png" width="160" height="24" alt="AddThis Feed Button" /></a>This 
						is an Atom feed of our <xsl:apply-templates select="atom:link[contains(concat(' ',@rel,' '),' via ')]" mode="via" />.
						For more information on feeds, see the <a href="http://en.wikipedia.org/wiki/Web_feed" title="Wikipedia - Web Feeds">wikipedia entry</a> or FeedBurner's <a href="http://www.feedburner.com/fb/a/feed101" title="FeedBurner - Feed 101">Feed 101</a>.
						To subscribe to this feed in your favorite reader, you can use the <a href="http://addthis.com/" title="Feed Widgets">AddThis</a> widget to the right.
						You can also get <xsl:apply-templates select="atom:link[@type='application/rss+xml']" mode="rss"/>.</p>
					<h2><xsl:apply-templates select="atom:subtitle"/></h2>
					<xsl:apply-templates select="atom:entry"/>
				</div>
				<div id="footer">
					<span>Last Modified:
						<xsl:call-template name="date">
							<xsl:with-param name="class">modified</xsl:with-param>
							<xsl:with-param name="date" select="atom:updated"/>
						</xsl:call-template>
					</span>
					<span class="generator">Generated by: <xsl:value-of select="atom:generator"/></span>
					<span class="copyright"><xsl:apply-templates select="atom:rights"/></span>
				</div>
			</body>
		</html>
	</xsl:template>
	<xsl:template match="atom:entry">
		<div class="project hentry">
			<h3 class="entry-title"><xsl:apply-templates select="atom:title"/></h3>
			<div class="entry-content">
				<xsl:apply-templates select="atom:content/xhtml:div/xhtml:p"/>
				<ul><xsl:apply-templates select="atom:link[contains(concat(' ',@rel,' '),' enclosure ')]" mode="enclosure"/></ul>
			</div>
			<dl>
				<dt>Permalink</dt>
				<dd class="permalink"><xsl:apply-templates select="atom:link[contains(concat(' ',@rel,' '),' alternate ')]" mode="permalink"/></dd>
				<dt>Started</dt>
				<dd>
					<xsl:call-template name="date">
						<xsl:with-param name="class">published</xsl:with-param>
						<xsl:with-param name="date" select="atom:published"/>
					</xsl:call-template>
				</dd>
				<dt>Updated</dt>
				<dd>
					<xsl:call-template name="date">
						<xsl:with-param name="class">updated</xsl:with-param>
						<xsl:with-param name="date" select="atom:updated"/>
					</xsl:call-template>
				</dd>
				<dt>Tagged</dt>
				<dd>
					<ul class="tags">
						<xsl:apply-templates select="atom:category"/>
					</ul>
				</dd>
				<dt>Authors</dt>
				<dd>
					<ul class="authors">
						<xsl:apply-templates select="../atom:author"/>
					</ul>
				</dd>
			</dl>
		</div>
	</xsl:template>
	<xsl:template match="atom:link" mode="self">
		<link rel="alternate" href="{@href}" type="{@type}" title="{../atom:title}"/>
	</xsl:template>
	<xsl:template match="atom:link" mode="alternate">
		<link rel="alternate" href="{@href}" type="{@type}" title="{../atom:title}"/>
	</xsl:template>
	<xsl:template match="atom:link" mode="via">
		<a href="{@href}" type="{@type}" title="{../atom:title}">portfolio</a>
	</xsl:template>
	<xsl:template match="atom:link" mode="rss">
		<a href="{@href}" title="{../atom:title}" type="{@type}">this feed in <acronym title="Really Simple Syndication">RSS</acronym> format</a>
	</xsl:template>
	<xsl:template match="atom:link" mode="enclosure">
		<li><img src="{@href}" alt="{@title}" title="{@title}" /></li>
	</xsl:template>
	<xsl:template match="atom:link" mode="permalink">
		<a href="{@href}" title="Permalink for {../atom:title}" rel="bookmark" type="{@type}"><xsl:value-of select="@href"/></a>
	</xsl:template>
	<xsl:template match="atom:author">
		<li><address class="author vcard"><a class="url fn" href="{atom:uri}"><xsl:value-of select="atom:name"/></a></address></li>
	</xsl:template>
	<xsl:template match="atom:category">
		<li><a href="{@scheme}{@term}" title="Tagged: {@term}" rel="tag"><xsl:value-of select="@label | @term"/></a></li>
	</xsl:template>
	<xsl:template match="xhtml:p">
		<p class="entry-summary"><xsl:copy-of select="node()"/></p>
	</xsl:template>
	<xsl:template name="date">
		<xsl:param name="class"/>
		<xsl:param name="date"/>
		<abbr>
			<xsl:if test="$class">
				<xsl:attribute name="class">
					<xsl:value-of select="$class"/>
				</xsl:attribute>
			</xsl:if>
			<xsl:if test="$date">
				<xsl:attribute name="title">
					<xsl:value-of select="$date"/>
				</xsl:attribute>
			</xsl:if>
			<xsl:choose>
				<xsl:when test="function-available('date:format-date')">
					<xsl:value-of select="date:format-date($date, 'EEEE, MMMM d, yyyy')"/>
				</xsl:when>
				<xsl:otherwise>
					<!-- <xsl:value-of select="$date"/> -->
					<xsl:call-template name="date:format-date">
						<xsl:with-param name="date-time" select="$date" />
						<xsl:with-param name="pattern" select="'EEEE, MMMM d, yyyy'" />
					</xsl:call-template>
				</xsl:otherwise>
			</xsl:choose>
		</abbr>
	</xsl:template>
</xsl:stylesheet>
